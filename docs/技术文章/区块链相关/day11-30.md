---
title: '关于智能合约的逻辑更新'
date: 2022-11-30 18:44:15
tags:
- '区块链技术'
- '智能合约'
categories:
- 'EVM'
---

<!-- more -->

::: tip

部署在链上的合约是不可更改的,这点不能改变。 :+1:

但是不是每次更新功能或者项目升级、迁移都需要重新部署一个合约然后让用户们自行迁移呢?:fast_forward:

这种方式无疑过于麻烦,而且成本高.

还是说我们可以在第一版合约就考虑绝大部分的情况设置足够数量的函数去修改合约参数呢?

先不说这种方式是不是设定了一个合约管理员让合约重新变得中心化的问题,更重要的问题在于你一开始就能考虑到所有以后业务的更改吗?

:::

::: tip

所以现在更新合约的方式一般有以下三种：

1.直接部署新合约,让用户自行迁移

2.类似 aave的方式做合约管理公约

3.最合理的方式：代理合约:accept:

:::

### 简单的代理合约代码演示（最小代理合约模式）:

```solidity
// SPDX-License-Identifier: MIT

pragma solidity 0.8.7;

/**
 * 在大型区块链项目中,设置代理合约对于项目的可扩展性和持续发展是重要的
 * 代理合约的作用类似与抽象类或顶层接口,只需要设置使用setImplementation方法切换真正实现逻辑的合约
 即可完成智能合约的更新,而不需要再重新发布一份新的合约.
 * 注意:
 * 1.存储的数据依然是归属在代理合约上的
 * 2.实现合约上不能有构造器函数,否则数据将会存储在实现合约上
 * 3.此处是最小代理合约所以不会出现函数选择器冲突问题(相同的函数选择器存在代理合约和功能合约中)
 * 但是存储冲突问题(因为存储的数据依然存在代理合约上的原因 实现的功能合约只能新增加存储变量而不能重新排序或者更改旧的变量) 是依然存在的
 *
 * 用户调用过程: userCall->代理合约(读取实现合约地址)->call对应合约方法->读取修改或增加的数据
 * 代理合约新增实现合约过程: 代理合约setImplementation(implementationAddress)
 */

import "@openzeppelin/contracts/proxy/Proxy.sol";

contract SmallProxy is Proxy {
    constructor() {}

    // This is the keccak-256 hash of "eip1967.proxy.implementation" subtracted by 1
    bytes32 private constant _IMPLEMENTATION_SLOT =
        0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;

    //设置新的实现合约地址
    function setImplementation(address newImplementation) public {
        assembly {
            sstore(_IMPLEMENTATION_SLOT, newImplementation)
        }
    }

    function _implementation()
        internal
        view
        override
        returns (address implementationAddress)
    {
        assembly {
            implementationAddress := sload(_IMPLEMENTATION_SLOT)
        }
    }

    //call调用 实现合约上逻辑实现的方法
    function getData(uint256 numUpdate) public pure returns (bytes memory) {
        return abi.encodeWithSignature("setVal(uint256)", numUpdate);
    }

    //读取被修改的数据 sload(0) 表示slot[0] 存储槽0
    function readStorage() public view returns (uint256 valAtStorageZero) {
        assembly {
            valAtStorageZero := sload(0)
        }
    }
}

contract ImplementTest {
    uint256 public val;

    function setVal(uint256 _val) public {
        val = _val;
    }
}

contract ImplementTest2 {
    uint256 public val;

    function setVal(uint256 _val) public {
        val = _val + 2;
    }
}

```


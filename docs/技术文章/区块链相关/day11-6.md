---
title: 'ERC20标准的代币实现'
date: 2022-11-6 18:44:15
tags:
- '区块链技术'
- '智能合约'
categories:
- 'EIP'
- 'ERC20'
---

<!-- more -->

::: tip

ERC20标准接口规范链接:https://eips.ethereum.org/EIPS/eip-20

::: 

#### 代码实现：

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.7;

interface tokenRecipient {
    function receiveApproval(
        address _from,
        uint256 _value,
        address _token,
        bytes calldata _extraData
    ) external;
}

/**
 * 首先什么是代币,代币并不像比特币、以太坊等native token拥有自己的区块链。它实际是运行在其他支持智能合约的区块链之上的智能合约,因此它叫代币
 * 什么是ERC20:首先ERC(以太坊征求意见提案)是EIP(以太坊题案)的一个子类,而20实际是题案序号.
 * 那么ERC20题案题了什么? 题的是同质化代币标准,与之相对的还有ERC721(非同质化代币标准)、ERC1155(兼容同质化与非同质化的标准)
 * 那么什么又是同质化与非同质化代币:首先一般性质与功能不变的就是同质化,比如(USDT,UIN,LINK)...都是只作为区块链等价物使用的
 * 而非同质化代币它是每个代币都有唯一性质的,比如NFT
 */

//ERC20标准代币创建合约
contract TestToken {
    // public类型变量:代币的名称、标识、精度、总代币数量
    string public name;
    string public symbol;
    uint8 public decimals = 18;

    uint256 public totalSupply;

    //表示代币持有的map结构
    mapping(address => uint256) public balanceOf;
    mapping(address => mapping(address => uint256)) public allowance; //允许其他合约或地址持有此合约创建的代币

    // 转账成功后触发Transfer事件
    event Transfer(address indexed from, address indexed to, uint256 value);

    // This generates a public event on the blockchain that will notify clients
    event Approval(
        address indexed _owner,
        address indexed _spender,
        uint256 _value
    );

    // This notifies clients about the amount burnt
    event Burn(address indexed from, uint256 value);

    /**
     * Constructor function
     *
     *构造合约代币的几项基本数据(初始提供代币,代币名称,代币标识)
     */
    constructor(
        uint256 initialSupply,
        string memory tokenName,
        string memory tokenSymbol
    ) {
        totalSupply = initialSupply * 10**uint256(decimals); // 初始提供代币*精度 == 目前供给的所有代币
        balanceOf[msg.sender] = totalSupply; // 合约部署者即代币创建者,初始获得所有代币
        name = tokenName; //代币名称
        symbol = tokenSymbol; //代币标识
    }

    /**
     * 合约内部转账函数. 有一点需要注意:因为代币它是依托于其他区块链上的,所以gas费用依然是消耗其所依托区块链的token
     */
    function _transfer(
        address _from,
        address _to,
        uint256 _value
    ) internal {
        // 不允许转账到地址为0的账户
        require(_to != address(0x0));
        // 确认发起转账地址有足够的代币
        require(balanceOf[_from] >= _value);
        //确认代币没有少转
        require(balanceOf[_to] + _value >= balanceOf[_to]);
        // 保存转账前两个地址相加的总余额作为测试回退条件
        uint256 previousBalances = balanceOf[_from] + balanceOf[_to];
        // 校验过后开始加减不同地址的余额
        balanceOf[_from] -= _value;
        balanceOf[_to] += _value;
        //触发记录转账事件
        emit Transfer(_from, _to, _value);
        // 断言转账成功,即转账后两个地址余额相加的总余额不会变
        assert(balanceOf[_from] + balanceOf[_to] == previousBalances);
    }

    /**
     * 外部调用转账函数
     *
     * Send `_value` tokens to `_to` from your account
     *
     * @param _to 接收转账地址
     * @param _value 转账金额
     */
    function transfer(address _to, uint256 _value)
        public
        returns (bool success)
    {
        _transfer(msg.sender, _to, _value);
        return true;
    }

    /**
     * 从其他地址转账到目标地址函数
     * 表示可以有一个代理人可以帮你转移代币:mapping(代理地址 => mapping(调用者的地址 => 余额))
     *
     * Send `_value` tokens to `_to` on behalf of `_from`
     *
     * @param _from 从哪个地址转账
     * @param _to 转账到哪个地址
     * @param _value 转账金额
     */
    function transferFrom(
        address _from,
        address _to,
        uint256 _value
    ) public returns (bool success) {
        require(_value <= allowance[_from][msg.sender]); // 验证地址余额
        allowance[_from][msg.sender] -= _value;
        _transfer(_from, _to, _value);
        return true;
    }

    /**
     * Set allowance for other address
     *
     * 允许 `_spender` 代理地址花费不多于 `_value`的代币(一般用于授权其他合约的使用)
     *
     * @param _spender 代理地址
     * @param _value 代理地址可以花费多少代币
     */
    function approve(address _spender, uint256 _value)
        public
        returns (bool success)
    {
        //设置代理地址最多允许使用金额
        allowance[msg.sender][_spender] = _value;
        //触发approval事件记录
        emit Approval(msg.sender, _spender, _value);
        return true;
    }

    /**
     * Set allowance for other address and notify
     *
     * 允许 `_spender` 代理地址花费不多于 `_value`的代币,并且通知代币创建合约记录 (一般用于授权其他合约的使用)
     *
     * @param _spender 代理地址
     * @param _value 代理地址可以花费多少代币
     * @param _extraData 说明信息发送到代币创建合约
     */
    function approveAndCall(
        address _spender,
        uint256 _value,
        bytes memory _extraData
    ) public returns (bool success) {
        //tokenRecipient接口
        tokenRecipient spender = tokenRecipient(_spender);
        //如果设置代理地址成功,将允许者,金额,代币创建合约地址,说明信息.记录到receiveApproval函数
        if (approve(_spender, _value)) {
            spender.receiveApproval(
                msg.sender,
                _value,
                address(this),
                _extraData
            );
            return true;
        }
    }

    /**
     * Destroy tokens
     *
     * 不可逆的删除调用者地址中的代币
     *
     * @param _value 要删除的代币数量
     */
    function burn(uint256 _value) public returns (bool success) {
        require(balanceOf[msg.sender] >= _value); // 确认地址中余额是否足够
        balanceOf[msg.sender] -= _value; //从发送地址中删除余额
        totalSupply -= _value; // 从总额中删除对应的余额
        //触发删除代币事件
        emit Burn(msg.sender, _value);
        return true;
    }

    /**
     * Destroy tokens from other account
     *
     * 删除目标地址的代币
     *
     * @param _from 删除代币的地址
     * @param _value 代币金额
     */
    function burnFrom(address _from, uint256 _value)
        public
        returns (bool success)
    {
        require(balanceOf[_from] >= _value); //检查目标地址中代币数量是否足够
        require(_value <= allowance[_from][msg.sender]); // 检查目标地址对应的代理/授权地址是否有足够的余额
        //删除代理地址以及原始地址的代币
        balanceOf[_from] -= _value;
        allowance[_from][msg.sender] -= _value;
        //从总额中删除对应的余额
        totalSupply -= _value;
        emit Burn(_from, _value);
        return true;
    }
}

```


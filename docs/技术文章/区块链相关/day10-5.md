---
title: 'solidity基础3'
date: 2022-10-5 14:44:15
tags:
- '区块链技术'
- '智能合约'
categories:
- 'solidity基础'



---

<!-- more -->

```solidity
// SPDX-License-Identifier: MIT 

pragma solidity ^0.8.7;

//1.get funds form users 2.withdraw funds 3. set a minumun funding value in usd
import "./PriceConver.sol";

//函数错误回退声明
error NotOwner();

contract fundMe{

//immutable类似于final 可以节省部署合约时的gas费用 与constant的区别在于immutable是在部署的时候确定值的而constant在编译时就确定了
 address public immutable i_owner;

//构造器函数 部署合约时执行
constructor(){
    //因为msg.sender在合约构造器中被调用,所以部署此合约的地址会 == owner
    i_owner = msg.sender;
}
  
//选定库中特定返回值的函数
 using PriceConver for uint256;

 uint256 constant MINIMUN_USD = 2 * 1e18;

 address[] public funders;
 mapping(address => uint256) public Amountfunders;

 //payable关键字表示此函数可做交易操作
 //合约部署完之后的地址与钱包地址一样也可以接受转账做交易操作
 function fund() public payable{
     //msg是全局变量.value表示数字货币数量 require限制数量函数。写法require(表达式，"错误信息") 
     //如果require函数返回错误用户的gas将原路退回,此函数其他操作也不会执行
     //引入库之后在参数后加入.调用库中函数 这种写法实际.前的参数会作为函数的第一个形参.而不是getPriceAndChange(msg.value)
     
      //1eth == 1^18 Wei == 1000000000000000000
     require(msg.value.getPriceAndChange() >= MINIMUN_USD, "didn't send enough");
     
     funders.push(msg.sender);//获取捐赠者的地址 
    
     Amountfunders[msg.sender] = msg.value.getPriceAndChange();
     } 

    
 function withdraw() public oneOwner {
     
     //循环捐赠地址数组将每一笔捐赠金额置空
    for(uint256 funderindex = 0; funderindex < funders.length; funderindex++){
          //将表中的金额设空  
          Amountfunders[funders[funderindex]]  = 0 ;
     }
      //三种合约转账函数 transfer send call
      
      //msg.sender是需要发送代币的地址 address(this)是此合约地址 最大发送使用的gas是2300
    // payable(msg.sender).transfer(address(this).balance);
     
     //send函数转账失败不会直接抛出异常 只会返回布尔值 所以需要require函数限制 最大发送使用的gas是2300
    // require(payable(msg.sender).send(address(this).balance),"send fail");  

     //call函数是一个底层函数 可以调用EVM中的几乎任何函数 只需传入形参即可 有两个返回值布尔值和信息 无gas限制
    (bool callSuccess ,) = payable(msg.sender).call{value:address(this).balance}("");
     require(callSuccess,"call fail"); 
  }

//自定义关键字 类似java中的自定义注解功能 
  modifier oneOwner{
      //使用require函数校验，方便抛出异常。但是这种方式比较浪费gas，因为每抛出一次异常都会重新存储一次异常信息字符串。
     // require(msg.sender == i_owner ,"withdraw fail sender Is not owner");
   
   //使用revert错误回退则会比较节省gas
   if(msg.sender != i_owner){
       revert NotOwner();
   }
   // _ 如果放在下面就表示此关键字内的逻辑最先执行 放在上面相反
     _;
  }

  //如果接收到msg.data(有任何数据调用)就会调用
  receive() external payable{
      //接收到任何数据(转账或者调用合约)调用主要功能函数fund
          fund(); 
  }
  //没有receive函数 或者调用合约错误就会调用 类似java中的全局异常处理
  fallback() external payable{
          fund();
  }
}
```

##### 库文件 PriceConver.sol

```solidity
// SPDX-License-Identifier: MIT 

pragma solidity ^0.8.7;

//直接从github引用接口
import "@chainlink/contracts/src/v0.8/interfaces/AggregatorV3Interface.sol";

//新建库文件 类似java中的工具类
library PriceConver{
     //解决智能合约（基础链系统）与外部系统（中心化服务器）数据交互问题 the oracle problem
     //因为需要保证千万不同节点的区块数据一致性问题,所以部署在区块链上的智能合约不能直接与外界的中心化服务器进行交互。
     //需要通过去中心化的预言机网络（二层链、扩展链）产生确定的数据与基础链上的智能合约交互
     function getPriceAndChange(uint256 ethAmount) internal view returns(uint256){
      //使用ChainLink ABI
      //此处的地址是ETH/USD转换函数实现的合约地址 AggregatorV3Interface是接口，返回5个参数所以不需要的参数加上逗号
      //得到的price为当前eth的usd价格 精度18位
     (,int256 price,,,) = AggregatorV3Interface(0xD4a33860578De61DBAbDc8BFdb98FD742fA7028e).latestRoundData();
     //当前价格(加18位长度的数字单位)*用户捐赠的eth数(精度8位)除以1^18
     return  (uint256( price * 1e10) * ethAmount) / 1e18;
     }

     function getfeedVersion() internal view returns(uint256){
           return AggregatorV3Interface(0xD4a33860578De61DBAbDc8BFdb98FD742fA7028e).version();
     }
}
```

